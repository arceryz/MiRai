// We require version 430 since it supports compute shaders.
#version 430

// This is the workgroup size. The largest size that is guaranteed by OpenGL 
// to available is 1024, beyond this is uncertain.
// Might influence performance but only in advanced cases.
layout(local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;

// Two buffers are here.
// The ray ssbo is packed per bounce, ie numRays * numBounces.
layout(std430, binding=0) buffer mirrorSSBO {  vec2 mirrorPoints[]; };
layout(std430, binding=1) buffer distanceSSBO { vec2 distances[]; };

layout(location=0) uniform int numRays;
layout(location=1) uniform int numBounces;
layout(location=2) uniform int numMirrors;
layout(location=3) uniform float arcFocus;
layout(location=4) uniform float time;
layout(location=5) uniform vec2 rayOrigin;
layout(location=6) uniform int bounceStart;
layout(location=7) uniform float cornerSize;

const float PI = 3.14159;
const float STEP_MIN = 0.001f;
const float FOCUS_INF = 1000.0f;

struct HitInfo {
    float t;
    vec2 point;
    vec2 normal;
};
HitInfo RayLineHit(vec2 o, vec2 d, vec2 u, vec2 v);
HitInfo RayCircleHit(vec2 o, vec2 d, vec2 cc, float rsq, bool interior);
float DistanceSq(vec2 a, vec2 b);

void main()
{
    // Index represents the point that we are casting.
    // This ranges from 0 to numRays.
    // We write distances for each particle at the numBounces consecutive positions.
    // That way we have multiple depth layers.
    uint index = gl_GlobalInvocationID.x;
    float prog = float(index) / numRays;

    // Ray originate from (0, 0).
    vec2 ray;
    //float rayAngle = 0.5*PI + 2*PI/10.0*prog;
    float rayAngle = 2*PI*prog;
    ray.x = cos(rayAngle);
    ray.y = sin(rayAngle);

    vec2 origin = rayOrigin;
    //origin = vec2(cos(time*600), sin(time*600))*0.05;
    bool hyperbolic = arcFocus < 0;

    for (int i = 0; i < numBounces; i++) {
        distances[index*numBounces+i] =  vec2(0);
    }

    // Trace all bounces and store the distance at each collision.
    float travelled = 0;
    for (int i = 0; i < numBounces; i++) {
        HitInfo hitmin;
        hitmin.t = -1;
        vec2 umin = vec2(0);
        vec2 vmin = vec2(0);

        // Evaluate every mirror plane.
        for (int j = 0; j < numMirrors; j++) {
            distances[index*numBounces+i] = vec2(0, 1);
            vec2 u = mirrorPoints[2*j];
            vec2 v = mirrorPoints[2*j+1];
            vec2 c = (u+v)/2;

            vec2 n = normalize(vec2(-v.y+u.y, v.x-u.x));
            bool doCurved = abs(arcFocus) < FOCUS_INF-1;

            HitInfo hit;
            if (doCurved) {  
                vec2 arc = n*arcFocus;
                float rsq = dot(arc-u, arc-u);
                hit = RayCircleHit(origin, ray, arc, rsq, !hyperbolic);
            }
            else {
                hit = RayLineHit(origin, ray, u, v);
            }
            
            // Hit condition: Point must lie between the two points.
            // The projection of point-u on v-u normalized must be between
            vec2 uv = v-u;
            vec2 up = hit.point-u;
            float pdir = dot(up, uv) / dot(uv, uv);
            float pnormal = dot(up, n);

            // Validity consists of proper step size. Point lying in between the two edges and
            // for circles the point must also lie in the proper direction.
            // focus > 0 then dot < 0, focus < 0 then dot > 0. I.e opposing sign.
            bool validity = hit.t > STEP_MIN && (0 <= pdir && pdir <= 1) && (pnormal*arcFocus <= 0 || !doCurved);

            if (validity && (hitmin.t < 0 || hit.t < hitmin.t)) {
                hitmin = hit;
                umin = u;
                vmin = v;
            }
        }
        
        // Only do something if there is a hit.
        if (hitmin.t > 0) {
            // Reflect.
            ray = ray - 2*hitmin.normal*dot(hitmin.normal, ray);
            origin = hitmin.point;
            travelled += hitmin.t;

            // Compute shortest distance to mirror plane corners.
            float dminsq = min(DistanceSq(umin, origin), DistanceSq(vmin, origin));

            // Write at bounce dependent index.
            // If numbounces changes, then the stride grows with it.
            if (i >= bounceStart) {
                vec2 dvec = vec2(travelled, sqrt(dminsq));
                distances[index*numBounces+i] = dvec;
            }
            if (dminsq < cornerSize*cornerSize && cornerSize > 0) break;
        } 
        else {
            break;
        }
    }

}

// This function collides a ray o+td with a line uv.
HitInfo RayLineHit(vec2 o, vec2 d, vec2 u, vec2 v) 
{
    HitInfo hit;
    hit.t = -1;

    // The normal is a left-rotated vector.
    vec2 n = normalize(vec2(-v.y+u.y, v.x-u.x));
    float nd = dot(n, d);
    if (nd == 0) return hit;
    
    hit.t = dot(n, u-o) / nd;
    hit.point = o + hit.t*d;
    hit.normal = n;
    return hit;
}

HitInfo RayCircleHit(vec2 o, vec2 d, vec2 cc, float rsq, bool interior)
{
    HitInfo hit;
    hit.t = -1;

    // Make the point relative to the circle.
    vec2 rel = cc-o;
    float dp = dot(rel, d);
    float D = dp*dp + rsq - dot(rel, rel);

    // We take an absolute value here, but if the
    // square root ends up negative we set t to zero.
    if (D < 0) return hit;
    float sqrtD = sqrt(D);
    float t1 = dp - sqrtD;
    float t2 = dp + sqrtD;

    hit.t = float(interior)*max(t1, t2) + (1-float(interior))*min(t1, t2);
    hit.point = o + hit.t*d;
    hit.normal = normalize(cc - hit.point);
    return hit;
}

float DistanceSq(vec2 a, vec2 b)
{
    return dot(a-b, a-b);
}